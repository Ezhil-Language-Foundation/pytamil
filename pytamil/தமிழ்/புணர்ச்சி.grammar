@@grammar::PUNARCHI

start
    =
    expression {expressions}*
    ;

expressions
    = 
    ',' @:expression 
    ;

expression =  nilaimozhiexpression operator varumozhiexpression;


nilaimozhiexpression
    = 
    nilaimozhi {filters}*
    ;

nilaimozhi
    =
    /[^|+]*/ # any character except '|' or '+'
    ;

varumozhiexpression
    =
    {filters}* varumozhi
    ;

varumozhi
    =
    /[^,]*/ # any character except '|'
    ;


filters
    = 
    | '|' @:filter
    | @:filter '|'
    ;


filter
    =
    | filtername param
    | filtername
    ;

param= '(' @:value ')' ;

filtername = 'உடம்படுமெய்' | 'இரட்டுதல்' | 'திரிதல்' | 'சும்மா' ;

operator
    = 
    | '+இயல்பு+'
    | '+'
    ;


value = /[^)]*/; # any character except ')'

